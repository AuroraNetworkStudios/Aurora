plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'maven-publish'
}

configurations {
    shade
}

group = 'gg.auroramc'
version = '1.1.2'

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    // WorldGuard/WorldEdit
    maven { url "https://maven.enginehub.org/repo/" }
    maven {
        name "essentialsx-releases"
        url "https://repo.essentialsx.net/releases/"
    }
    // Multiverse core
    maven { url = "https://repo.onarandombox.com/content/groups/public/" }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"

    // 3rd party
    compileOnly "me.clip:placeholderapi:2.11.3"
    compileOnly 'net.essentialsx:EssentialsX:2.19.0'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.9'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly 'com.onarandombox.multiversecore:multiverse-core:4.3.12'
    compileOnly "com.github.angeschossen:WildRegenerationAPI:1.5.0"

    // 3rd party local
    compileOnly name: 'CMI9.0.0.0API'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // HikariCP
    implementation("com.zaxxer:HikariCP:5.1.0") {
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    // JUnit Jupiter API and Engine dependencies
    testImplementation "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

test {
    useJUnitPlatform()
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        jvmArgs()
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier = ''
    // Include dependency from the 'shade' configuration into the JAR
    configurations = [project.configurations.runtimeClasspath]

    // Relocate the exp4j package to prevent any conflicts with other versions that might be on the classpath
    relocate "com.zaxxer.hikari", "gg.auroramc.aurora.libs.hikari"
}

tasks {
    build {
        dependsOn shadowJar
    }
}

def publishProperties = rootProject.file('publish.properties')

if (publishProperties.exists()) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(publishProperties))
    // Set the properties as project properties
    properties.each { key, value ->
        project.ext.set(key, value)
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact("build/libs/Aurora-$version" + ".jar") {
                    extension 'jar'
                }
            }
        }

        repositories {
            maven {
                name = 'AuroraMC'
                url "https://repo.auroramc.gg/repository/maven-releases/"
                credentials {
                    username project.ext.username
                    password project.ext.password
                }
            }
        }
    }
}


